// Tests process handles, destructuring stdout/stderr/exit_code, and background handles.

const ensure_module = import("test_support/ensure")
const ensure = ensure_module.ensure

const { stdout: src_listing, stderr: src_errors, exit_code } = ls ./src
ensure(exit_code == 0, "ls exited cleanly")
ensure(src_errors == "", "stderr is empty for successful sync command")
ensure(src_listing != "", "stdout captured interpreter files")

const background = echo "background-complete" &
const finished = await background
ensure(finished.status.ok, "background process resolved successfully")
ensure(finished.stdout == "background-complete\n", "await returned buffered stdout")

if background |stdout, stderr, status| {
  ensure(status.ok, "if-capture awaited background process implicitly")
  ensure(stderr == "", "captured stderr remains empty")
  ensure(stdout == "background-complete\n", "captured stdout matches awaited value")
}

echo "process handles test passed"
